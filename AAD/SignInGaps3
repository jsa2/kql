let mass = materialize (SigninLogs 
| where tostring(AuthenticationDetails) !has "First factor requirement satisfied by claim in the token"
| where NetworkLocationDetails !has "trustedNamedLocation"
| where ConditionalAccessStatus == "success"
| where Status.additionalDetails != "MFA requirement satisfied by claim in the token" and Status.additionalDetails != "MFA requirement skipped due to remembered device" and Status.additionalDetails != "MFA completed in Azure AD"
| where AuthenticationRequirement has "singleFactorAuthentication"
| where TimeGenerated > now()-90d);
let mass2 = mass
| extend identifier = iff(isempty(DeviceDetail.browser), UserAgent, DeviceDetail.operatingSystem)
| extend trueId = case(identifier contains "ios", "ios", identifier contains "Android", "Android", "other");
let rs = materialize(mass2
| extend ID = strcat(trueId, '-',AppDisplayName)
| extend CaDetail = ConditionalAccessPolicies
|mv-expand CaDetail);
let stage2 = rs
| summarize correlation = make_set(strcat(CaDetail.result, '-',CorrelationId), 5) by ID, ClientAppUsed;
let stage3 = rs
| extend pols = strcat(CaDetail.result, "-",CaDetail.displayName, "-", CaDetail.enforcedGrantControls)
| summarize make_set(pols) by ID;
let stage4 =  rs
| summarize make_set(UserDisplayName) by ID
| extend usersCount = array_length(set_UserDisplayName)
| project-away set_UserDisplayName;
 stage2 
| join kind=inner  stage3 on ID
| project ID, correlation, set_pols
| join kind=innerunique  stage4 on ID
| project ID, usersCount, correlation, set_pols
| extend fullBypass = case(tostring(set_pols) has "failure", "false", tostring(set_pols) has "success", "false", "true")
//



// Check for certain devicePlats
let mass = materialize (SigninLogs 
| where AppDisplayName !contains "" and AppDisplayName !contains ""
| where tostring(AuthenticationDetails) !has "First factor requirement satisfied by claim in the token"
| where NetworkLocationDetails !has "trustedNamedLocation"
| where ConditionalAccessStatus == "success"
| where Status.additionalDetails != "MFA requirement satisfied by claim in the token" and Status.additionalDetails != "MFA requirement skipped due to remembered device" and Status.additionalDetails != "MFA completed in Azure AD"
| where AuthenticationRequirement has "singleFactorAuthentication"
| where TimeGenerated > now()-7d);
let mass2 = materialize ( mass
| extend identifier = iff(isempty(DeviceDetail.browser), UserAgent, DeviceDetail.operatingSystem)
| extend trueId = case(identifier contains "ios", "ios", identifier contains "Android", "Android", "other")
| where trueId != "other");
let stage1 = mass2
| summarize make_set(UserPrincipalName) by AppDisplayName, ClientAppUsed
| extend userCount = array_length(set_UserPrincipalName)
| project-away set_UserPrincipalName;
let stage2 = mass2
| extend CaDetail = ConditionalAccessPolicies
|mv-expand CaDetail
| summarize make_set(strcat(CaDetail.result, '_', CorrelationId), 5) by AppDisplayName;
stage1
| join stage2 on AppDisplayName
